plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
    id "androidx.navigation.safeargs.kotlin"
    id 'io.sentry.android.gradle'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "me.codeenzyme.wally"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def wallyPropertiesFile = rootProject.file "wally.properties"
    def wallyProperties = new Properties()
    wallyProperties.load new FileInputStream(wallyPropertiesFile)

    buildFeatures {
        viewBinding true
    }

    buildTypes {

        debug {
            buildConfigField("String", "BASE_API_URL", wallyProperties["DEV_API_BASE_URL"])
            buildConfigField("String", "API_KEY", wallyProperties["API_KEY"])
        }

        release {
            buildConfigField("String", "BASE_API_URL", wallyProperties["PROD_API_BASE_URL"])
            buildConfigField("String", "API_KEY", wallyProperties["API_KEY"])
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    // Photo Viewer
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    // Paging 3
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    // Navigation Component
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    // Datastore preference library
    implementation "androidx.datastore:datastore-preferences:$datastore_version"

    // Glide image loading library
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Retrofit HTTP library
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // OkHttp logging interceptor
    implementation "com.squareup.okhttp3:logging-interceptor:$logging_interceptor_version"

    // Timber logging library
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Sentry crashlytics
    implementation "io.sentry:sentry-android:$sentry_version"

    // Shimmer
    implementation "com.facebook.shimmer:shimmer:$shimmer_version"

    // Image Cropper
    api "com.theartofdev.edmodo:android-image-cropper:$image_cropper"

    // Dexter
    implementation "com.karumi:dexter:$dexter_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}